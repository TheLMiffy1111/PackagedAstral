plugins {
	id "net.neoforged.gradle" version "[6.0.13,)"
	id "org.parchmentmc.librarian.forgegradle" version "[1,)"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

repositories {
	jcenter()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://repo.spongepowered.org/maven/"
	}
	maven {
		url = "https://maven.tterrag.com/"
	}
	maven {
		url = "https://maven.blamejared.com/"
	}
	maven {
		url = "https://maven.theillusivec4.top/"
	}
	maven {
		url = "https://maven.hellfiredev.net/"
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

version = "2.0.2.7"
group = "thelm.packagedastral"
archivesBaseName = "PackagedAstral-1.16.5"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "parchment", version: "2022.03.06-1.16.5"
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			mods {
				packagedastral {
					source sourceSets.main
				}
			}
		}
		client
		server
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.16.5-36.2.39"

	implementation fg.deobf("appeng:appliedenergistics2:8.4.7")
	implementation fg.deobf("top.theillusivec4.curios:curios-forge:1.16.5-4.0.8.2")
	implementation fg.deobf("hellfirepvp.observerlib:observerlib:1.16.5-1.5.3.v78") {
		transitive = false
	}
	implementation fg.deobf("hellfirepvp.astralsorcery:astralsorcery-1.16.5:1.16.4-1.14.1.v106") {
		transitive = false
	}
	implementation fg.deobf("mezz.jei:jei-1.16.5:7.8.0.1009")
	implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.2.521")
	implementation fg.deobf("vazkii.patchouli:Patchouli:1.16.4-53.3")
	implementation fg.deobf("com.jozufozu.flywheel:Flywheel:1.16-0.2.5.53")
	implementation fg.deobf("com.simibubi.create:Create:mc1.16.5_v0.3.2g+480")
	implementation fg.deobf("mekanism:Mekanism:1.16.5-10.1.2.457")
	implementation fg.deobf("mekanism:Mekanism:1.16.5-10.1.2.457:generators")

	//CurseMaven
	implementation fg.deobf("curse.maven:packagedauto-308380:4678908")
	implementation fg.deobf("curse.maven:ato-405593:3405905")
}

jar {
	manifest {
		attributes([
			"Specification-Title": "PackagedAstral",
			"Specification-Vendor": "thelm",
			"Specification-Version": "2",
			"Implementation-Title": "${archivesBaseName}",
			"Implementation-Version": "${version}",
			"Implementation-Vendor": "thelm",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		],)
	}
}
